!>
................................................................................
.    Copyright (c) 2009-2025 Crater Dog Technologies.  All Rights Reserved.    .
................................................................................
.  DO NOT ALTER OR REMOVE COPYRIGHT NOTICES OR THIS FILE HEADER.               .
.                                                                              .
.  This code is free software; you can redistribute it and/or modify it under  .
.  the terms of The MIT License (MIT), as published by the Open Source         .
.  Initiative. (See https://opensource.org/license/MIT)                        .
................................................................................
<!

!>
BALI DOCUMENT NOTATION
This document contains a formal definition of the Bali Document Notation™ (Bali)
using Crater Dog Syntax Notation™ (CDSN).

For more information on CDSN see the wiki at:
  - https://github.com/craterdog/go-syntax-notation/wiki

┌──────────────────────────────────────────────────────────────────────────────┐
│                               RULE DEFINITIONS                               │
└──────────────────────────────────────────────────────────────────────────────┘
<!
$Document: comment? Component

$Component: Entity Parameters?

$Entity:
  - Element
  - String
  - Range
  - Collection
  - Procedure

$Parameters:
  - MultilineParameters
  - InlineParameters

$MultilineParameters: "(" newline AnnotatedAssociation+ ")"

$AnnotatedAssociation: Association note?

$InlineParameters: "(" Association AdditionalAssociation* ")"

$AdditionalAssociation: "," Association

$Association: symbol colon Component

$Element:
  - angle
  - boolean
  - citation
  - duration
  - moment
  - number
  - pattern
  - percentage
  - probability
  - resource

$String:
  - binary
  - bytecode
  - name
  - narrative
  - quote
  - symbol
  - tag
  - version

$Range: LowerBound Primitive ".." Primitive UpperBound

$LowerBound:
  - LowerInclusion
  - LowerExclusion

$LowerInclusion: "[|"

$LowerExclusion: "(|"

$Primitive:
  - Element
  - String

$UpperBound:
  - UpperInclusion
  - UpperExclusion

$UpperInclusion: "|]"

$UpperExclusion: "|)"

$Collection:
  - MultilineAttributes
  - MultilineValues  ! Must be after the multiline attributes.
  - NoAttributes
  - NoValues
  - InlineAttributes
  - InlineValues  ! Must be after the inline attributes.

$MultilineAttributes: "[" newline AnnotatedAssociation+ "]"

$InlineAttributes: "[" Association AdditionalAssociation* "]"

$NoAttributes: "[" colon "]"

$MultilineValues: "[" newline AnnotatedValue+ "]"

$AnnotatedValue: Component note?

$InlineValues: "[" Component AdditionalValue* "]"

$AdditionalValue: "," Component

$NoValues: "[" "]"

$Procedure:
  - MultilineStatements
  - InlineStatements
  - NoStatements

$MultilineStatements: "{" newline AnnotatedStatement+ "}"

$AnnotatedStatement:
  - EmptyLine
  - AnnotationLine
  - StatementLine

$EmptyLine: newline

$AnnotationLine:
  - note
  - comment

$StatementLine: Statement note?

$InlineStatements: "{" Statement AdditionalStatement* "}"

$AdditionalStatement: ";" Statement

$NoStatements: "{" "}"

$Statement: MainClause OnClause?

$MainClause:
  - Flow
  - Induction
  - Messaging
  - Repository

$OnClause: "on" Failure MatchHandler+

$MatchHandler: "matching" Template "do" Procedure

$Failure: symbol

$Template: Expression

$Flow:
  - IfClause
  - SelectClause
  - WhileClause
  - WithClause
  - ContinueClause
  - BreakClause
  - ReturnClause
  - ThrowClause

$Induction:
  - DoClause
  - LetClause

$Messaging:
  - PostClause
  - RetrieveClause
  - AcceptClause
  - RejectClause
  - PublishClause

$Repository:
  - CheckoutClause
  - SaveClause
  - DiscardClause
  - NotarizeClause

$IfClause: "if" Condition "do" Procedure

$Condition: Expression

$SelectClause: "select" Target MatchHandler+

$Target:
  - Function
  - Method
  - Subcomponent
  - Variable  ! This must be last since others also begin with an identifier.

$Function: identifier "(" Arguments? ")"

$Arguments: Argument AdditionalArgument*

$AdditionalArgument: "," Argument

$Argument: identifier

$Method: identifier Threading identifier "(" Arguments? ")"

$Threading:
  - dot
  - arrow

$Variable: identifier

$WhileClause: "while" Condition "do" Procedure

$WithClause: "with" "each" Item "in" Sequence "do" Procedure

$Item: symbol

$Sequence: Expression

$ContinueClause: "continue" "loop"

$BreakClause: "break" "loop"

$ReturnClause: "return" Result

$Result: Expression

$ThrowClause: "throw" Exception

$Exception: Expression

$DoClause: "do" Invocation

$Invocation:
  - Function
  - Method

$LetClause: "let" Recipient Assign Expression

$Assign:
  - colonEqual
  - defaultEqual
  - plusEqual
  - dashEqual
  - starEqual
  - slashEqual

$Recipient:
  - symbol
  - Subcomponent

$PostClause: "post" Message "to" Bag

$Message: Expression

$Bag: Expression

$RetrieveClause: "retrieve" Recipient "from" Bag

$AcceptClause: "accept" Message

$RejectClause: "reject" Message

$PublishClause: "publish" Event

$Event: Expression

$CheckoutClause: "checkout" Recipient AtLevel? "from" Citation

$AtLevel: "at" "level" Expression

$Citation: Expression

$SaveClause: "save" Draft "as" Citation

$Draft: Expression

$DiscardClause: "discard" Draft

$NotarizeClause: "notarize" Draft "as" Citation

$Expression: Subject Predicate*

$Subject:
  - Component
  - Subcomponent
  - Precedence
  - Referent
  - Complement
  - Inversion
  - Magnitude
  - Function
  - Method
  - Variable  ! This must be last since others also begin with an identifier.

$Subcomponent: identifier "[" Indices "]"

$Indices: Index AdditionalIndex*

$AdditionalIndex: "," Index

$Index: Expression

$Predicate: Operator Expression

$Operator:
  - plus
  - dash
  - star
  - slash
  - double
  - caret
  - less
  - equal
  - more
  - ampersand
  - Is
  - Matches
  - And
  - San
  - Ior
  - Xor

$Is: "is"

$Matches: "matches"

$And: "and"

$San: "san"

$Ior: "ior"

$Xor: "xor"

$Precedence: "(" Expression ")"

$Referent: snail Indirect

$Indirect:
  - Component
  - Subcomponent
  - Referent
  - Function
  - Method
  - Variable  ! This must be last since others also begin with an identifier.

$Complement: "not" Logical

$Logical:
  - Component
  - Subcomponent
  - Precedence
  - Referent
  - Complement
  - Function
  - Method
  - Variable  ! This must be last since others also begin with an identifier.

$Inversion: Inverse Numerical

$Inverse:
  - dash
  - slash
  - star

$Numerical:
  - Component
  - Subcomponent
  - Precedence
  - Referent
  - Inversion
  - Magnitude
  - Function
  - Method
  - Variable  ! This must be last since others also begin with an identifier.

$Magnitude: bar Numerical bar

!>
┌──────────────────────────────────────────────────────────────────────────────┐
│                            EXPRESSION DEFINITIONS                            │
└──────────────────────────────────────────────────────────────────────────────┘
<!
$alpha: ['A'..'Z' 'a'..'z']

$alphanumeric: alpha | base10

$ampersand: '&'

$angle: '~' (zero | amplitude | "pi" | "π" | "tau" | "τ")

$authority: ~['/' CONTROL]+

$bar: '|'

$base10: ['0'..'9']

$base16: base10 | ['a'..'f']

$base32: base10 | ['A'..'D' 'F'..'H' 'J'..'N' 'P'..'T' 'V'..'Z']

$base64: alphanumeric | ['+' '/']

$binary: "'>" (EOL block+)? space* "<'"

$block: space* base64{1..60} EOL

$boolean: "false" | "true"

$bytecode: "'" (instruction (space instruction)*)* "'"

$caret: '^'

$character: escape | ~['"' CONTROL]

$citation: name slash version

$colonEqual: ":="

$colon: ":"

$comment: "!>" EOL (ANY | EOL)* EOL space* "<!" EOL  ! Chooses the shortest match.

$dashEqual: "-="

$day: (['0'..'2'] ['1'..'9']) | ('3' ['0'..'1'])

$days: timespan 'D'

$defaultEqual: "?="

$double: "//"

$duration: '~' sign? 'P' (weeks | (years? months? days? ('T' hours? minutes? seconds?)?))

$equal: "="

$escape: '\' (unicode | ['a' 'b' 'f' 'n' 'r' 't' 'v' '"' '\'])

$exponent: 'E' sign? ordinal

$float: sign? amplitude

$fraction: dot base10+

$fragment: ~['>' CONTROL]*

$hour: (['0'..'1'] ['0'..'9']) | ('2' ['0'..'3'])

$hours: timespan 'H'

$imaginary: sign? amplitude 'i'

$infinity: sign? ("infinity" | '∞')

$instruction: base16{4}

$letter: LOWER | UPPER

$amplitude: (zero fraction | ordinal fraction?) exponent?

$matches: "matches"

$minute: ['0'..'5'] ['0'..'9']

$minutes: timespan 'M'

$moment: '<' sign? year ('-' month ('-' day ('T' hour (':' minute (':' second fraction?)?)?)?)?)? '>'

$month: ('0' ['1'..'9']) | ('1' ['0'..'2'])

$months: timespan 'M'

$more: ">"

$name: (slash identifier)+

$narrative: '"' '>' EOL (ANY | EOL)* EOL space* '<' '"' EOL  ! Chooses the shortest match.

$note: "! " ~[CONTROL]*

$ordinal: ['1'..'9'] base10*

$path: ~['?' '#' '>' CONTROL]*

$pattern: "none" | regex | "any"

$percentage: real '%'

$probability: fraction | "one"

$plusEqual: "+="

$plus: '+'

$polar: amplitude "e^" angle? 'i'

$query: ~['#' '>' CONTROL]*

$quote: '"' character* '"'

$real: zero | float | infinity | undefined

$rectangular: float sign float? 'i'

$regex: '"' character+ '"' '?'

$resource: '<' scheme ':' ("//" authority)? '/' path ('?' query)? ('#' fragment)? '>'

$scheme: alpha (alphanumeric | plus | dash | dot)*

$second: (['0'..'5'] ['0'..'9']) | ('6' ['0'..'1'])

$seconds: timespan 'S'

$sign: plus | dash

$slashEqual: "/="

$slash: '/'

$snail: '@'

$space: ' '

$starEqual: "*="

$star: '*'

$symbol: '$' identifier

$tag: '#' base32+

$timespan: zero | (ordinal fraction?)

$undefined: "undefined"

$unicode: ('u' base16{4}) | ('U' base16{8})

$version: 'v' ordinal (dot ordinal)*

$weeks: timespan 'W'

$year: zero | ordinal

$years: timespan 'Y'

$zero: '0'

$number: polar | rectangular | imaginary | real ! Must be after percentage.

$identifier: letter (letter | DIGIT)*  ! Must be after other identifiers.

$dot: '.'  ! Must be after probability.

$dash: '-'  ! Must be after number.

$arrow: "<-"  ! Must be after moment.

$less: "<"  ! Must be after other expressions beginning with '<'.

